---
version: 1.3.1
title: Containers
---

_Tuples_, _Lists_, _Assoc Lists_, _Sets_, _Maps/Hashmaps_, et _Vectors_. (Les termes techniques ne sont volontairement pas traduits et seront affich√©s en _italique_)

{% include toc.html %}

## Quand utiliser tel ou tel _container_ ?

Voici une liste de pense-b√™tes permettant de choisir le bon _container_ en fonction du contexte. 
L'objectif est mn√©motechnique et fera surement plus sens apr√®s avoir lu les chapitres correspondants.

- Besoin d'associer simplement des donn√©es de diff√©rents types ? Utilisons un _[Tuple](#tuples)_
- Besoin d'ajouter des √©l√©ments au d√©but de _container_ (_prepend_) de mani√®re performante ? Utilisons une _[List](#lists)_
- Besoin de garantir l'unicit√© et l'ordre des √©l√©ments ? Utilisons un _[Set](#sets)_
- Besoin de rechercher une valeur √† partir d'une cl√© ? Utilisons une _[Map](#maps)_
- Besoin d'ind√©xer des donn√©es de mani√®re performante ? Utilisons un _[Vector](#vectors)_

## Utiliser les modules (_List_, _Set_, _Map_)

Une fonctionnalit√© appr√©ciable des modules _List_, _Set_ et _Map_ est leur _api_ identique (ou presque). Ainsi, l'utilisation de ces _containers_ est plus intuitive et plus simple √† m√©moriser, permettant un acc√®s rapide √† un large √©ventail d'outils pour un investissement relativement faible.

La contre-partie est l'exposition √† des conflits dans nos imports (_namespace collisions_). C'est une pratique commune et recommand√©e d'utiliser des imports nomm√©s (_qualified imports_). Si vous suivez ce chapitre avec votre interpr√©teur `ghci`, nous vous invitons √† importer les modules comme d√©crit ci-dessous, ainsi tous les modules seront correctement disponibles.

```haskell
ghci> import Data.Set as Set
ghci> import Data.Map as Map
ghci> import Data.List.NonEmpty as NE
ghci> :t Map.empty
Map.empty :: Map k a
ghci> :t Set.empty
Set.empty :: Set.Set a
ghci> :t NE.head
NE.head :: NE.NonEmpty a -> a
```

Afin de se familiariser avec les messages d'erreur, en pr√©vision du jour o√π ils arriveront (et ce jour arrivera :wink:), voici ce qui se produit sans import nomm√© (_unqualified imports_)

```haskell
ghci> import Data.List
ghci> import Data.Set
ghci> import Data.Map
ghci> lookup

<interactive>:4:1: error:
    Ambiguous occurrence ‚Äòlookup‚Äô
    It could refer to
       either ‚ÄòData.Map.lookup‚Äô,
              imported from ‚ÄòData.Map‚Äô
              (and originally defined in ‚ÄòData.Map.Internal‚Äô)
           or ‚ÄòPrelude.lookup‚Äô,
              imported from ‚ÄòPrelude‚Äô (and originally defined in ‚ÄòGHC.List‚Äô)
ghci> empty

<interactive>:5:1: error:
    Ambiguous occurrence ‚Äòempty‚Äô
    It could refer to
       either ‚ÄòData.Map.empty‚Äô,
              imported from ‚ÄòData.Map‚Äô
              (and originally defined in ‚ÄòData.Map.Internal‚Äô)
           or ‚ÄòData.Set.empty‚Äô,
              imported from ‚ÄòData.Set‚Äô
              (and originally defined in ‚ÄòData.Set.Internal‚Äô)
```

## _Tuples_

Les _Tuples_ sont la premi√®re structure de donn√©e que vous allez d√©couvrir en Haskell. C'est une structure de donn√©e, simple, primitive, avec une syntaxe native et concise. Les champs sont r√©f√©renc√©s selon leurs positions. Th√©oriquement, Les _Tuples_ peuvent contenir un nombre infini de champs, c'est ce qu'on appelle *arit√©* (_arity_). En r√©alit√©, les sp√©cifications _Haskell Report_ n'imposent aux compilateurs (et interpr√©teurs) une taille minimale que de 15 champs (_15-tuple_). `GHC` supporte un nombre de champs allant jusqu'√† 62 (_62-tuple_). Le nombre minimum de champs pour un _tuple_ est de 2 (_2-tuple_). C'est sur ce type que nous nous concentreront √©tant donn√© qu'Haskell fournit de nombreuses fonctionnalit√©s par d√©faut pour celui-ci. A titre d'exemple, voici un _tuple_ avec 8 champs (_8-tuple_).

```haskell
ghci> :t ('0', '1', '2', '3', '4', '5', '6', "8-tuple")
('0', '1', '2', '3', '4', '5', '6', "8-tuple")
  :: (Char, Char, Char, Char, Char, Char, Char, [Char])
```
__Note__: `:t` dans l'interpr√©teur ghci affiche le type de la valeur donn√©e. Il affichera le message sous la forme `valeur :: Type`.

### Quand l'utiliser ?

Un _Tuple_ est utile pour associer des donn√©es, √©ventuellement h√©t√©rog√®nes : "J'ai besoin d'associer ces √©lements". Il n'a pas de valeur *s√©mantique* (sens), mais se concentre sur la *syntaxe* (structure), c'est pourquoi c'est souvent d√©conseill√© d'en abuser. Si la structure de donn√©e √† cr√©er est utilis√©e √† plusieurs endroits de l'application, il est pr√©f√©rable d'utiliser un _Record_. Au contraire, si l'utilisation est isol√©e et locale (interne √† une fonction par exemple) alors un _Tuple_ peut √™tre appropri√©e!

### Comment le cr√©er ?

Un _Tuple_ peut √™tre cr√©√© en utilisant les parenth√®ses avec chaque √©l√©ment s√©par√© par une virgule `(a, b)`.

```haskell
ghci> myTuple = (True, "hello")
ghci> :t myTuple
myTuple :: (Bool, [Char])
```

On peut √©galement laisser un champ vide, il se transforme alors en fonction. Cette technique est appel√© _tuple sectioning_. Elle requiert l'extension `TupleSections`.

```haskell
ghci> :set -XTupleSections
ghci> :t (True,)
(True,) :: t -> (Bool, t)
ghci> :t (,"hello")
(,"hello") :: t -> (t, String)
ghci> :t (,)
(,) :: a -> b -> (a, b)
ghci> (,) True "hello"
(True,"hello")
```
__Note__: Pour activer une extension du langage (_language extension_) depuis l'interpr√©teur ghci on utilise le mot-cl√© `:set` qui permet de d√©finir une option, le prefixe `-X` permet de d√©clarer que c'est une extension du langage qui est directement suivi du nom de l'extension en pascal case.

### Comment le manier ?

La principale mani√®re d'utiliser un _tuple_ est en le d√©composant. L'approche la plus commune est d'utiliser le _pattern matching_ sur la structure du _tuple_ et d'acc√©der √† son contenu. Cette technique est particuli√®rement pratique parce qu'elle fonctionne pour tous types (taille) de tuples.

```haskell
ghci> (\(a, b) -> not a) myTuple
False
ghci> (\(a, b) -> b <> " world") myTuple
"hello world"

ghci> (\(a, b, c) -> a <> " " <> b <> " " <> c) ("my", "name", "is")
"my name is"
```

Avec un _tuple_ d'arit√© de 2 (_2-tuple_), les fonctions `fst` et `snd` sont disponibles dans la biblioth√®que standard. Les _2-tuples_ sont les seuls _tuples_ pour lesquels ces fonctions sont disponibles. Pour les autres _tuples_, il faudra √©crire ses propres fonctions pour acc√©der aux √©lements voulus.

```haskell
ghci> :t fst
fst :: (a, b) -> a
ghci> fst myTuple
True
ghci> :t snd
snd :: (a, b) -> b
ghci> snd myTuple
"hello"
```

### Quelles limitations ?

La principale limitation des _tuples_ en Haskell est que chaque chaque arit√© correspond √† un type disctinct. Il n'y a donc pas de fonction commune pour ajouter un √©l√©ment √† un _tuple_. Ces fonctions doivent donc √™tre √©crites ad hoc (express√©ment pour chaque usage).

Voici un exemple pour augmenter la taille du _tuple_

```haskell
ghci> twoTupleToThreeTuple c (a, b) = (a, b, c)
ghci> twoTupleToThreeTuple () myTuple
(1, "world", ())
```

Tenter d'appeler notre fonction sur un _tuple_ d'arit√© diff√©rente conduira √† une erreur de typage laconique; la fonction attend un _2-tuple_ mais on lui a fournit un _3-tuple_.

```haskell
ghci> twoTupleToThreeTuple True (1, 2, 3)

<interactive>:19:27: error:
    ‚Ä¢ Couldn't match expected type: (a, b)
                  with actual type: (a0, b0, c0)
                  ...
```

## _Lists_

En terme d'utilisation, les _Lists_ permettent de r√©soudre le probl√®me d'extension qu'on a observer avec les _Tuples_ (capacit√© √† augmenter la taille du _container_ sans cr√©er une nouvelle instance). Par contre, une _List_ ne peut contenir qu'un unique type de donn√©e (elles sont dites homog√®nes). Les _Lists_ sont construites avec une syntaxe sp√©cifique : les crochets avec des virgules s√©parant chaque √©l√©ments.

```haskell
ghci> [1,2,3,4]
[1,2,3,4]
```

### Types inductif/r√©cursif (_Inductive Types_)

Les _Lists_ sont la premi√®re introduction aux types dit "inductif", une sous-cat√©gorie des types "recursifs" (nous y reviendrons dans les concepts avanc√©s). Voici un exemple identique √† l'impl√©mentation en Haskell sans le sucre syntaxique.

```haskell
data List a = Nil | Cons a (List a)
```

On peut voir que ce type est recursif. `Nil` est la valeur de base, le constructeur `Cons` y "adjoint" `a` et appelle r√©cursivement `List a`. On peut √©galement constater pourquoi les _Lists_ ne peuvent contenir qu'un seul type de donn√©es. En effet, la r√©cursion ne porte que sur un type de donn√©es `a`. Dasn notre d√©finition on peut remplacer `Nil` par une _List_ vide `[]` et `Cons` par `:` pour retrouver la syntaxe par d√©faut.

Voici plusieurs exemples √©quivalents pour illustrer cette m√©canique : la cr√©ation par d√©faut d'une _List_, la cr√©ation avec le constructeur `:`, et une cr√©aton manuelle avec le constructeur `Cons`.

```haskell
ghci> [1,2,3,4]
[1,2,3,4]
ghci> 1 : 2 : 3 : 4 : []
[1,2,3,4]
ghci> Cons 1 (Cons 2 (Cons 3 (Cons 4 Nil)))
Cons 1 (Cons 2 (Cons 3 (Cons 4 Nil)))
```

### Quand l'utiliser ?

Les _Linked lists_ sont des structures de donn√©es tr√®s pr√©sentes en programmation fonctionnelle, par cons√©quent vous y serez tr√®s souvent confront√© en Haskell. C'est en g√©n√©ral le premier _container_ auquel on pense. De part, la faible performence de l'ajout d'√©l√©ment en fin de _List_ (_append_) et la relative faible performance d'acc√®s par index (ùõ∞(n) avec n l'index), les _Lists_ sont g√©n√©ralement utilis√©es lorsque l'on sait que l'on va devoir it√©rer sur les √©lements de celles-ci et o√π l'ordre des √©lements est important.

Un bon exemple d'utilisation des _Linked lists_ : impl√©menter une _Stack_ parce que l'ajout et le retrait d'√©l√©ment se font avec une complexit√© ùõ∞(1) (_Last In First Out_).

Un mauvais exemple d'utilisation des _Linked lists_ : impl√©menter une _Queue_ parce que l'ajout ou le retrait d'√©lement se fait avec une complexit√© ùõ∞(n) (_First In First Out_).

Un exemple concret que l'on rencontre souvent dans des applications est la cr√©ationd de requ√™tes en base de donn√©es. Une requ√™te peut retourner soit aucun r√©sultat `[]` soit un certain nombre potentiellement ordonn√©s`[entity..]`. Les biblioth√®ques d'acc√®s aux base de donn√©es ont rarement besoin de garantir un acc√®s par index performant, ainsi elles laissent cette responsabilit√© √† la fonction appelante.

### Concatenation de _Lists_

Pour concater deux listes (ou plus), on utilise l'op√©rateur `++` :

```haskell
ghci> [1, 2] ++ [3, 4, 1]
[1, 2, 3, 4, 1]
```

### Les fonctions utilitaires : _Head_ / _Tail_

Lorsque l'on utilise des _Lists_, il est commun de travailler avec le premier √©l√©ment (_head_) et le reste des √©l√©ments (_tail_).

Haskell fournit deux fonctions simples pour acc√©der √† ces √©l√©ments de la liste : `head` et `tail` :

```haskell
ghci> head ["Orange", "Banana", "Apple"]
"Orange"
ghci> tail ["Orange", "Banana", "Apple"]
["Banana","Apple"]
```

Malheureusement ces fonctions ont un d√©savantage dans la biblioth√®que de base; elles peuvent lever des exceptions m√™me lorsqu'on leur fournit un √©l√©ment dont le type est correct. La cause de ces exceptions est que ces deux fonctions ne g√®rent pas l'ensemble des cas possibles (_partial functions_) : exemple une liste vide.

```haskell
ghci> head []
*** Exception: Prelude.head: empty list
ghci> tail []
*** Exception: Prelude.tail: empty list
```

Pour couvrir ces possibilit√©s de mani√®re sure, il est commun en Haskell d'utiliser le type `Maybe`. Il permet de notifier au programme que les cas qui ne sont pas couverts doivent retourner `Nothing`. L'appelant de la fonction retournant un type _maybe_ est alors forc√© de g√©rer les cas o√π `Nothing` sera retourn√© mais se pr√©munit des exceptions lors de l'√©xecution du programme (_runtime exceptions_).

```haskell
ghci> :i Maybe
data Maybe a = Nothing | Just a 	-- Defined in ‚ÄòGHC.Maybe‚Äô
...
```
__Note__: `:i` dans l'interpr√©teur ghci donne des informations √† propos du type, la premi√®re ligne est son impl√©mentation.

On peut √† pr√©sent d√©finir des fonctions _head_ et _tail_ sures, prenant en compte tous les cas possibles sans exceptions, (_total functions_) en utilisant le _pattern matching_!

```haskell
ghci> :{
| safeHead :: [a] -> Maybe a
| safeHead [] = Nothing
| safeHead (x:xs) = Just x
|
| safeTail :: [a] -> Maybe [a]
| safeTail [] = Nothing
| safeTail (x:xs) = Just xs
| :}
ghci> safeHead ["Orange", "Banana", "Apple"]
Just "Orange"
ghci> safeHead []
Nothing
ghci> safeTail ["Orange", "Banana", "Apple"]
Just ["Banana","Apple"]
ghci> safeTail []
Nothing
```
__Note__: `:{` et `:}` permettent d'√©crire des d√©finition sur plusieures lignes dans l'interpr√©teur ghci.

Youpi! Plus d'exceptions.

Une autre mani√®re de s'assurer que `head` et `tail` sont sures est d'utiliser les _NonEmpty Lists_ :

```haskell
ghci> import Data.List.NonEmpty
ghci> :i NonEmpty
data NonEmpty a = a .:| [a]
```

En regard de plus pr√®s la d√©finition, on s'aper√ßoit que `NonEmpty` oblige la pr√©sence d'un premier √©l√©ment. Le symbole `:|` est un constructeur comme `:`, dans les faits la d√©finition de `NonEmpty` est identique √† la d√©finition des _Lists_ vues pr√©c√©demment, √† la nuance pr√™t qu'elle omet le cas `[]` (_Nil_).

Cette mani√®re aborde le probl√®me avec une approche oppos√©e √† la solution pr√©c√©dente avec `Maybe`. Plut√¥t que de forcer l'appelant de se pr√©munir des cas manquants lorsqu'il re√ßoit le r√©sultat, elle force l'appelant √† construire sa liste avec des donn√©es valides (lorsqu'il appelle la fonction).

```haskell
ghci> import Data.List.NonEmpty as NE
ghci> :t NE.head
NE.head :: NonEmpty a -> a
ghci> head (1 :| [])
1
ghci> NE.head []
<interactive>:11:9: error:
    ‚Ä¢ Couldn't match expected type: NonEmpty a
                  with actual type: [a]
    ...
```

Il faut noter que cette erreur n'est pas une erreur d'ex√©cution (_runtime error_) mais une erreur de typage (_type error_), le compileur ghc nous pr√©vient que l'on tente d'utiliser une liste potentiellement vide au lieu du type de liste requis `NonEmpty`.

### Consid√©ration sur les performances

Haskell impl√©mente les liste comme des _Linked Lists_. Le constructeur `:` (appel√© _cons_ abbr√©g√© de _constructor_) sert de lien (_link_) entre les √©l√©ments de la liste. Ce choix a pour cons√©quences que certaines op√©rations soient rapides et d'autres lentes : 

Ajouter un √©l√©ment au d√©but d'une liste est facile et rapide. Il suffit de "lier" un nouvel √©l√©ment √† la liste existante avec le constructeur `:`.

```haskell
prepend value list = value : list
```

Par contre, √©tant donn√© que la liste peut √™tre vide (`[]` ou `Nil`) ou qu'un √©l√©ment peut √™tre li√© au reste de la liste (comme on l'a vu pr√©c√©demment), celle-ci ne comporte pas d'information sur sa taille ou de r√©f√©rence vers la fin de la liste.

Par cons√©quent, pour r√©cup√©rer la longueur d'une liste on doit i√©trer sur l'ensemble de celle-ci pour compter le nombre d'√©l√©ment. Pour trouver un √©l√©ment √† un index sp√©cifique, on doit traverser la liste jusqu'√† l'atteindre.

De m√™me, pour ajouter un √©l√©ment √† la fin d'une liste existante on doit ajouter un constructeur √† la fin de celle-ci et "lier" l'√©l√©ment :

```haskell
append originalList newList =
    case originalList of
        [] -> newList
        x : xs -> x : append xs newList
```

La fonction `append` definit ci-dessus est identique √† l'op√©rateur `++`, il faut donc √™tre prudent lorsque l'on veut ajouter un √©l√©ment en fin de liste. Une concat√©nation avec `++` dans une boucle donne une compl√©xit√© de ùõ∞(n¬≤) !

En raison de l'impl√©mentation avec des _Linked List_, la majeure partie des op√©rations sur les listes ont une complexit√© lin√©aire en temps (_linear time complexity_, `ùõ∞(n)`). Dans la plupart des cas ces op√©rations sont plus lentes qu'avec d'autres _containers_, une bonne raison d'apprendre √† connaitre chacun avec leurs avantages et leurs inconv√©nients !

## _Assoc(iation) lists_

Jusqu'√† maintenant on a vu l'acc√®s aux valeurs d'une _List_ √† partir de ses indexes ou aux valeurs d'un _Tuple_ par _pattern matching_. Cependant, un des cas d'utilisation les plus r√©pandus dans l'utilisation des _containers_ est le stockage de paires cl√©s-valeurs (_Key-Value_). Les _Assoc(iation) Lists_ (_assoc_ dans la lit√©rature) permettent ce stockage en combinant _List_ et _Tuple_ (_2-tuple_). Etant donn√© que ces structures de donn√©es ne sont que la combinaison des deux autres, la seule chose n√©cessaire est une fonctionalit√© de recherche (_lookup_) qui est fournit par le `Data.List` de la biblioth√®que par d√©faut.   

```haskell
ghci> import Data.List as List
ghci> assoc = [("foo", True), ("bar", False)]
ghci> :t assoc
assoc :: [(String, Bool)]
ghci> :t List.lookup
List.lookup :: Eq a => a -> [(a, b)] -> Maybe b
ghci> List.lookup "foo" assoc
Just True
ghci> List.lookup "bar" assoc
Just False
ghci> List.lookup "baz" assoc
Nothing
```

On retrouve ici la gestion du cas d'erreur en cas d'absence de r√©sultat gr√¢ce au type `Maybe`. En effet, il est toujours *possible* de rechercher une cl√© absente de la liste. Dans ce cas, aucun r√©sultat ne peut √™tre retourner et on obtient la valeur `Nothing`.

Il est int√©ressant de noter que la contrainte `Eq a` sur la cl√© permet √† la fonction de recherche _lookup_ de r√©aliser une comparaison d'√©galit√© entre le crit√®re de recherche et les cl√©s de la liste.

Tandis que les _assoc lists_ sont une premi√®re introduction aux _containers_ de type cl√©-valeur comme elles sont construites √† partir des types vus pr√©c√©demment, elles ne sont en r√©alit√© que peu utiles au quotidien (il y a des solutions plus adapt√©es). Comme √©voqu√© pr√©c√©demment, les listes ne sont pas tr√®s performantes pour les fonctions de recherches et la complexit√© asymptotique est, dans le pire cas de figure, lin√©aire `ùõ∞(n)`. Les _assoc lists_ sont souvent utilis√©es comme structures de donn√©es interm√©diaires avec pour objectif d'√™tre transform√©es en `Map`. La conversion en elle m√™me a une complexit√© de `ùõ∞(n*log n)` mais la recherche par cl√© sur une `Map` se fera alors avec une complexit√© de `ùõ∞(log n)`. Le co√ªt de conversion, relativement important, est alors tr√®s vite rentabilis√© √† l'utilisation.

## _Sets_

Les _Sets_ sont des _containers_ tr√®s int√©ressants, le principe central d'un _Set_ est l'appartenance (_membership_). Il est commun de cr√©er un _Set_ afin de pouvoir tester ult√©rieur si une valeur en fait partie.

Un _Set_ ne peut √™tre construit qu'en ins√©rant des √©lements dans un _Set_ vide :

```haskell
ghci> import Data.Set as Set
ghci> :t Set.empty
Set.empty :: Set a
ghci> Set.empty
fromList []
ghci> Set.insert 1 (Set.insert 2 (Set.insert 3 Set.empty))
fromList [1,2,3]
```

ou √† partir d'une _List_ :

```haskell
ghci> Set.fromList [4,3,2,1]
fromList [1,2,3,4]
```

On peut noter que les √©l√©ments ont √©t√© tri√©s apr√®s la cr√©ation du `Set`. L'impl√©mentation concr√®te des _Sets_ en Haskell sont des _binary trees_, qui d√©pendent de la capacit√© des donn√©es √† √™tre tri√©es (_orderable_). On peut voir cette contrainte `Ord a` dans la d√©finition des fonctions du type `Set` comme `insert` ou `fromList` par exemple.

```haskell
ghci> :t Set.insert
Set.insert :: Ord a => a -> Set a -> Set a
ghci> :t Set.fromList
Set.fromList :: Ord a => [a] -> Set a
ghci> :t Set.member
Set.member :: Ord a => a -> Set a -> Bool
```
Les _Sets_ ont une proprit√© tr√®s utile : il ne peut pas y avoir de duplication en leur sein. Si on ins√®re plusieurs fois la m√™me valeur, il ne se passe rien.

```haskell
ghci> insert1 = Set.insert 1
ghci> insert1 Set.empty
fromList [1]
ghci> insert1 (insert1 Set.empty)
fromList [1]
ghci> insert1 (insert1 (insert1 Set.empty))
fromList [1]
```
Passons √† la pratique avec un cas d'utilisation concret.

```haskell
ghci> evens = Set.fromList [0,2..1000000]
ghci> Set.member 7 evens
False
ghci> Set.member 200012 evens
True
ghci> isEven n = Set.member n evens
ghci> isEven 7
False
ghci> isEven 8
True
```

On peut se dire "hmmm une limite √† `1000000` pour les nombres pairs est incorrect", et ce serait pertinent! Ceci met en √©vidence une caract√©ristique des _Sets_ en Haskell, elles sont dites "finies" de part la rigueur n√©cessaire des structure de donn√©es bas niveau permettant de les cr√©er. C'est une diff√©rence notable avec les _Lists_ qui sont potentiellement "infinies" et dites _lazy_ (√©valu√©es √† l'utilisation plut√¥t qu'√† la cr√©ation).

### Op√©rations sur les _Sets_

La fonction `difference` du module `Data.Set` est un bon moyen de s√©parer un _Set_ en nouveaux _Sets_ √† partir de la notion d'appartenance (_membership_) de ses √©l√©ments avec un autre _Set_. Plus simplement, cette fonction retourne un _Set_ avec tous les √©l√©ments du premier _Set_ en retirant les √©l√©ments pr√©sents dans le second _Set_.

```haskell
ghci> set1 = Set.fromList ["a", "b", "c", "1", "2", "3"]
ghci> letters = Set.fromList ["a", "b", "c"]
ghci> nums = Set.fromList ["1", "2", "3"]
ghci> Set.difference set1 letters
fromList ["1","2","3"]
ghci> Set.difference set1 nums
fromList ["a","b","c"]
```

La fonction `union` va combiner les √©l√©ments (sans duplication) de deux _Sets_.

```haskell
ghci> nums
fromList ["1","2","3"]
ghci> letters
fromList ["a","b","c"]
ghci> Set.union nums letters
fromList ["1","2","3","a","b","c"]
ghci> set1
fromList ["1","2","3","a","b","c"]
ghci> Set.union nums set1
fromList ["1","2","3","a","b","c"]
```

La fonction `intersection` va permettre de r√©cup√©rer les √©l√©ments communs aux deux _Sets_.

```haskell
ghci> Set.intersection nums letters
fromList []
ghci> Set.intersection nums set1
fromList ["1","2","3"]
ghci> Set.intersection letters set1
fromList ["a","b","c"]
ghci> Set.intersection (fromList [1, 2]) (fromList [2, 3])
fromList [2]
```

`isSubsetOf` va permettre de v√©rifier si l'ensemble des √©l√©ments d'un _Set_ sont contenu dans un autre _Set_.

```haskell
ghci> Set.isSubsetOf letters set1
True
ghci> Set.isSubsetOf nums set1
True
ghci> Set.isSubsetOf nums letters
False
ghci> Set.isSubsetOf set1 nums
False
```

Tout _Set_ est un contenu par lui-m√™me.

```haskell
ghci> Set.isSubsetOf nums nums
True
ghci> Set.isSubsetOf set1 set1
True
ghci> Set.isSubsetOf letters letters
True
```

## _Maps_

En Haskell, les _Maps_ sont le _container_ prvil√©gi√© pour le stockage de donn√©es sous forme cl√©-valeur, parfois √©galement appel√©es dictionnaires (_dictionnaries_)

Une `Map` n√©cessite que ses cl√©s respectent une contrainte de tri (`Ord k`) tout comme une `Set` pour ses valeurs. La raison est la m√™me que pour les _Sets_, un _balanced binary tree_ est utilis√© comme structure de donn√©es dans l'impl√©mentation bas-niveau du type `Map` en Haskell, qui n√©cessite cette capacit√© √† trier les cl√©s.

Le type `Map` et les fonctions qui permettent d'int√©ragir avec sont import√©s du module `Data.Map` qui fait partie du _package_ `containers`. Pas d'inqui√©tude pour les d√©pendances √† ce stade, _containers_ est une biblioth√®que centrale et est int√©gr√©e dans l'interpr√©teur _ghci_.

Comme les _sets_, une _map_ doit √™tre contruite √† partir d'une _map_ vide en inserant des paires de cl√©-valeur :

```haskell
ghci> import Data.Map (Map)
ghci> import qualified Data.Map as Map
ghci> :t Map.empty
Map.empty :: Map k a
ghci> Map.empty
fromList []
ghci> Map.insert "a" 'a' (Map.insert "b" 'b' (Map.insert "c" 'c' Map.empty))
fromList [("a",'a'),("b",'b'),("c",'c')]
```

ou cr√©er √† partir d'une liste :

```haskell
ghci> Map.fromList [(4, '4'), (3, '3'), (2, '2'), (1,'1')]
fromList [(1,'1'),(2,'2'),(3,'3'),(4,'4')]
```

## Updating Values to a Map

A useful function to be aware of is `adjust`. This lets us update a value at a
specified key, only if it exists, if not the old map is returned.

```haskell
ghci> Map.adjust (+2) "first" oneItem
fromList [("first",3)]
ghci> :t Map.adjust
Map.adjust :: Ord k => (a -> a) -> k -> Map k a -> Map k a
ghci> Map.adjust (+2) "second" oneItem
fromList [("first",1)]
```

The observant reader will notice that `adjust` doesn't actually update the map.
The second invocation of adjust returns the original `oneItem` map, this makes
sense when you consider that all data in Haskell is immutable!

## When to use Maps

Maps are really great for in memory persistence of state that will need to be
retrieved by a key of some arbitrary type. This is is because maps have great
lookup asymptotics (`ùõ∞(log n)`) due to the ordering on the key values. The
example that immediately springs to mind is session storage on the server in a
web application. The session state can be indexed by an id that is stored in
the cookie, and the session state can be retrieved from an in memory `Map` on
every request. This solution doesn't scale infinitely, but you would be
surprised how well it works!

## HashMaps

There are some cases where we do not have an ordering on our type, but still
want to use it as a key to index a map. In this case we probably want to reach
for a hashmap. A hashmap simply hashes the key, et voil√†, we have an ordering!
This does require that the key type is hashable though.

The module that exports the `HashMap` data type and functionality is called
`Data.HashMap.Strict`, it lives in the `unordered-containers` package. The api
is identical to `Map` aside from a `Hashable k` constraint instead of an `Ord k`
on the key.
